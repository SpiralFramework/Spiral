lexer grammar OpenSpiralLexer;
import LibLexer, OSLBacktickLexer, OSLCommonLexer, OSLWordScriptLexer;

HEADER_DECLARATION: O S L INLINE_WHITESPACE S C R I P T (INLINE_WHITESPACE V OPT_INLINE_WHITESPACE SEMANTIC_VERSION)?;

SEMICOLON_SEPARATOR: (';' NEW_LINE? OPT_WHITESPACE);
NL_SEPARATOR: (NEW_LINE OPT_WHITESPACE);

ASSIGN_VARIABLE_NAME: (V A L | V A R) INLINE_WHITESPACE VARIABLE_NAME_IDENTIFIER;
VARIABLE_ASSIGNMENT: OPT_INLINE_WHITESPACE '=' OPT_INLINE_WHITESPACE;

VARIABLE_REFERENCE: '$' VARIABLE_NAME_IDENTIFIER;
SEMANTIC_VERSION: V OPT_INLINE_WHITESPACE INTEGER (OPT_INLINE_WHITESPACE '.' OPT_INLINE_WHITESPACE INTEGER (OPT_INLINE_WHITESPACE '.' OPT_INLINE_WHITESPACE INTEGER)?)?;

VALUE_SEPARATOR: OPT_INLINE_WHITESPACE ',' OPT_INLINE_WHITESPACE;

NAME_IDENTIFIER: (BACKTICK | NAME_START_IDENTIFIER) (BACKTICK | NAME_END_IDENTIFIER)*;

DIALOG_DRILL_NAME: D I A L O G INLINE_WHITESPACE F O R INLINE_WHITESPACE;

BASIC_DRILL_SEPARATOR: OPT_INLINE_WHITESPACE '|' OPT_INLINE_WHITESPACE;
DIALOGUE_SEPARATOR: OPT_INLINE_WHITESPACE ':' OPT_INLINE_WHITESPACE;

fragment NEW_LINE: '\r'? '\n';

fragment OPT_WHITESPACE: [ \t\r\n]*;
fragment OPT_INLINE_WHITESPACE: INLINE_WHITESPACE_CHARACTERS*;

fragment INLINE_WHITESPACE: INLINE_WHITESPACE_CHARACTERS+;

fragment NAME_START_IDENTIFIER: [a-zA-Z_];
fragment NAME_END_IDENTIFIER: [a-zA-Z0-9_\- ]+;
fragment LOCALISED_NAME_IDENTIFIER: VARIABLE_NAME_IDENTIFIER ('.' VARIABLE_NAME_IDENTIFIER)*;