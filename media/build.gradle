plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

allprojects {
    dependencies {
        compile project(":core")

        compileInclude "io.humble:humble-video-noarch:0.3.0"
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceSets {
        main {
            java {
                srcDirs = []
            }
            kotlin {
                srcDirs = []
            }
            resources {
                srcDirs = []
            }
        }

        test {
            java {
                srcDirs = []
            }
            kotlin {
                srcDirs = []
            }
            resources {
                srcDirs = []
            }
        }
    }

    shadowJar {
        mergeServiceFiles()
        baseName = "spiral"
        appendix = "media"
        classifier = "$project.name"
        version = jar.version

        from project(":media").sourceSets.main.output

        configurations = [project.configurations.compileInclude]

        manifest {
            attributes 'Humble-Native-App': 'humblevideo'
            attributes 'Humble-Native-Paths': '.'
            attributes 'Humble-Native-Root': '.'
        }
    }
}

task buildLibs()

subprojects { Project subProject ->
    task copyArtifacts {
        doLast {
            def DIST_DIR = "${project(":media").buildDir}${File.separator}dist"
            def SRC_DIR = "${subProject.buildDir}${File.separator}libs"

            ant.mkdir(dir: DIST_DIR)
            ant.copy(todir: DIST_DIR) {
                fileset(dir: SRC_DIR,
                        includes: "spiral-media*.jar")
            }
        }
    }

    afterEvaluate {
        def shadowJarTask = subProject.tasks.find { it.name == 'shadowJar' }
        if (shadowJarTask) {
            shadowJarTask.finalizedBy(copyArtifacts)
            buildLibs.dependsOn(shadowJarTask)
        }
    }
}