plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
}

ext {
    artifact_group = 'info.spiralframework'
    project_version = "${System.getenv('GIT_COMMIT') ?: "unknown"}".substring(0, 7) //'1.0.0-alpha'

    jackson_version = '2.9.4'
    sfl4j_version = '1.7.25'
    imperator_version = '8ef2bb7'
    logback_version = '1.2.3'
    fuel_version = '2.0.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    group = artifact_group
    version = project_version
    sourceCompatibility = 1.8

    jar {
        baseName = "spiral"
        appendix = "$project.name"
        version = ""
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        baseName = jar.baseName
        appendix = jar.appendix
        version = jar.version
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        baseName = jar.baseName
        appendix = jar.appendix
        version = jar.version
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        repositories {
            maven {
                // change to point to your repo, e.g. http://my.org/repo
                url = "$buildDir/repo"
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId = "spiral-$project.name"
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://maven.abimon.org"
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

subprojects {
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.10"
        compile "org.jetbrains.kotlin:kotlin-reflect:1.3.10"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}
