lexer grammar OpenSpiralLexer;
import OSLBacktickLexer, OSLCommonLexer, OSLWordScriptLexer;

HEADER_DECLARATION: 'OSL Script' OPT_INLINE_WHITESPACE;

SEMICOLON_SEPARATOR: (';' NEW_LINE? OPT_WHITESPACE);
NL_SEPARATOR: (NEW_LINE OPT_WHITESPACE);

BASIC_DRILL_CODE: INTEGER BASIC_DRILL_SEPARATOR;
BASIC_DRILL_NAME
    : (BACKTICK | DRILL_START_IDENTIFIER) (BACKTICK | DRILL_IDENTIFIER)* BASIC_DRILL_SEPARATOR
    ;

ASSIGN_VARIABLE_NAME: (S E T | A S S I G N | V A L | V A R) INLINE_WHITESPACE VARIABLE_NAME_IDENTIFIER;
VARIABLE_ASSIGNMENT: OPT_INLINE_WHITESPACE '=' OPT_INLINE_WHITESPACE;

VARIABLE_REFERENCE: '$' VARIABLE_NAME_IDENTIFIER;
SEMANTIC_VERSION: V OPT_INLINE_WHITESPACE INTEGER (OPT_INLINE_WHITESPACE '.' OPT_INLINE_WHITESPACE INTEGER (OPT_INLINE_WHITESPACE '.' OPT_INLINE_WHITESPACE INTEGER)?)?;

VALUE_SEPARATOR: OPT_INLINE_WHITESPACE ',' OPT_INLINE_WHITESPACE;

fragment NEW_LINE: '\r'? '\n';

fragment OPT_WHITESPACE: [ \t\r\n]*;
fragment OPT_INLINE_WHITESPACE: INLINE_WHITESPACE_CHARACTERS*;

fragment INLINE_WHITESPACE: INLINE_WHITESPACE_CHARACTERS+;

fragment BASIC_DRILL_SEPARATOR: '|';
fragment DRILL_START_IDENTIFIER: [a-zA-Z_];
fragment DRILL_IDENTIFIER: [ a-zA-Z0-9_]+;
fragment LOCALISED_NAME_IDENTIFIER: VARIABLE_NAME_IDENTIFIER ('.' VARIABLE_NAME_IDENTIFIER)*;